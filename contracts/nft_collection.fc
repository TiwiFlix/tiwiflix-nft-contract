#include "imports/stdlib.fc";
#include "imports/op-codes.fc";
#include "imports/params.fc";
;; NFT collection smart contract

;; storage scheme
;; default#_ royalty_factor:uint16 royalty_base:uint16 royalty_address:MsgAddress = RoyaltyParams;
;; storage#_ owner_address:MsgAddress next_item_index:uint64
;;           ^[collection_content:^Cell common_content:^Cell]
;;           nft_item_code:^Cell
;;           royalty_params:^RoyaltyParams
;;           minting_fee:uint64
;;           is_verified:uint1
;;           nft_item_amount:uint64
;;           max_supply:uint64
;;           = Storage;

(slice, int, cell, cell, cell, int, int, int, int) load_data() inline {
  var ds = get_data().begin_parse();
  return
    (ds~load_msg_addr(), ;; owner_address
     ds~load_uint(64), ;; next_item_index
     ds~load_ref(), ;; content
     ds~load_ref(), ;; nft_item_code
     ds~load_ref(),  ;; royalty_params
     ds~load_uint(64), ;; minting_fee
     ds~load_uint(1), ;; is_verified
     ds~load_uint(64), ;; nft_item_amount
     ds~load_uint(64) ;; max_supply
     );
}

() save_data(slice owner_address, int next_item_index, cell content, cell nft_item_code, cell royalty_params, int minting_price, int is_verified, int nft_item_amount, int max_supply) impure inline {
  set_data(begin_cell()
    .store_slice(owner_address)
    .store_uint(next_item_index, 64)
    .store_ref(content)
    .store_ref(nft_item_code)
    .store_ref(royalty_params)
    .store_uint(minting_price, 64)
    .store_uint(is_verified, 1)
    .store_uint(nft_item_amount, 64)
    .store_uint(max_supply, 64)
    .end_cell());
}

cell calculate_nft_item_state_init(int item_index, cell nft_item_code) {
  cell data = begin_cell().store_uint(item_index, 64).store_slice(my_address()).end_cell();
  return begin_cell().store_uint(0, 2).store_dict(nft_item_code).store_dict(data).store_uint(0, 1).end_cell();
}

slice calculate_nft_item_address(int wc, cell state_init) {
  return begin_cell().store_uint(4, 3)
                     .store_int(wc, 8)
                     .store_uint(cell_hash(state_init), 256)
                     .end_cell()
                     .begin_parse();
}

() deploy_nft_item(int item_index, cell nft_item_code, int total_coins, cell nft_content, slice sender_address) impure {
    var (owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, is_verified, nft_init_amount, max_supply) = load_data();
    
    ;; Ensure the minting price + storage is covered if the sender is not the owner
    if (equal_slices(owner_address, sender_address) != true) {
        throw_unless(403, total_coins >= minting_price + nft_init_amount);
    }
    
    ;; Deploy the NFT with fixed amount
    cell state_init = calculate_nft_item_state_init(item_index, nft_item_code);
    slice nft_address = calculate_nft_item_address(workchain(), state_init);
    var msg = begin_cell()
              .store_uint(0x18, 6)
              .store_slice(nft_address)
              .store_coins(nft_init_amount)
              .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
              .store_ref(state_init)
              .store_ref(nft_content);
    send_raw_message(msg.end_cell(), 1); ;; Pay transfer fees separately

    ;; Transfer the minting fee to the owner, if any, and if sender is not owner
    if ((minting_price > 0) & (equal_slices(owner_address, sender_address) != true)){
        var fee_msg = begin_cell()
                      .store_uint(0x18, 6) ;; bounce message
                      .store_slice(owner_address)
                      .store_coins(minting_price)
                      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers
                      .end_cell();
        send_raw_message(fee_msg, 1);
    }
    
    ;; Calculate Refund
    ;; refund = total - mint_price - nft_init - gas_safety
    int gas_safety = 20000000; ;; 0.02 TON
    int cost = nft_init_amount + gas_safety;
    if (equal_slices(owner_address, sender_address) != true) {
        cost += minting_price;
    }
    
    int refund_amount = total_coins - cost;
    
    if (refund_amount > 0) {
       var refund_msg = begin_cell()
                      .store_uint(0x10, 6) ;; no bounce
                      .store_slice(sender_address)
                      .store_coins(refund_amount)
                      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                      .store_uint(0, 32) ;; Text comment opcode
                      .store_slice("Excess refunded")
                      .end_cell();
       send_raw_message(refund_msg, 2); ;; Ignore errors
    }
}


() send_royalty_params(slice to_address, int query_id, slice data) impure inline {
  var msg = begin_cell()
    .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool packages:MsgAddress -> 011000
    .store_slice(to_address)
    .store_coins(0)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_uint(op::report_royalty_params(), 32)
    .store_uint(query_id, 64)
    .store_slice(data);
  send_raw_message(msg.end_cell(), 64); ;; carry all the remaining value of the inbound message
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    var (owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, is_verified, nft_item_amount, max_supply) = load_data();

    if (op == op::get_royalty_params()) {
        send_royalty_params(sender_address, query_id, royalty_params.begin_parse());
        return ();
    }

    ;;modification - anybody can deploy
    if (op == 1) { ;; deploy new nft
      throw_if(407, next_item_index >= max_supply);

      int item_index = in_msg_body~load_uint(64);
      throw_unless(402, item_index <= next_item_index);

      ;; Extract the total amount of coins from the message
      ;; int total_coins = in_msg_body~load_coins(); ;; Skip body coins, use msg_value
      in_msg_body~load_coins(); 

      ;; Extract the nft_content
      cell nft_content = in_msg_body~load_ref();

      ;; Check if the item_index is the last one
      var is_last = item_index == next_item_index;

      ;; Call deploy_nft_item with msg_value
      deploy_nft_item(item_index, nft_item_code, msg_value, nft_content, sender_address);

      if (is_last) {
          next_item_index += 1;
          save_data(owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, 1, nft_item_amount, max_supply); ;; set verified
      }
      return ();
    }

    throw_unless(401, equal_slices(sender_address, owner_address));

    if (op == 2) { ;; batch deploy of new nfts
      int counter = 0;
      cell deploy_list = in_msg_body~load_ref();
        ;; var (item_index, item, f?) = deploy_list~udict::delete_get_min(64);

      do {
        var (item_index, item, f?) = deploy_list~udict::delete_get_min(64);
        if (f?) {
          counter += 1;
          if (counter >= 80) { ;; Limit due to limits of action list size (decreased to 80 for safety)
            throw(399);
          }

          throw_unless(403 + counter, item_index <= next_item_index);
          throw_if(407, item_index >= max_supply);
          
          deploy_nft_item(item_index, nft_item_code, item~load_coins(), item~load_ref(), sender_address);
          if (item_index == next_item_index) {
            next_item_index += 1;
          }
        }
      } until ( ~ f?);
      save_data(owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, 1, nft_item_amount, max_supply); ;; set verified
      return ();
    }
    if (op == 3) { ;; change owner
      slice new_owner = in_msg_body~load_msg_addr();
      save_data(new_owner, next_item_index, content, nft_item_code, royalty_params, minting_price, is_verified, nft_item_amount, max_supply);
      return ();
    }

    if (op == 4) { ;; change content and royalty params
      save_data(owner_address, next_item_index, in_msg_body~load_ref(), nft_item_code, in_msg_body~load_ref(), minting_price, is_verified, nft_item_amount, max_supply);
      return ();
    }
    ;; Allow owner to update minting price
  if (op == 5) { ;; Change minting price
    int new_minting_price = in_msg_body~load_uint(64);
    save_data(owner_address, next_item_index, content, nft_item_code, royalty_params, new_minting_price, is_verified, nft_item_amount, max_supply);
    return ();
  }

  ;; Change NFT item amount (storage + safety buffer for item)
  if (op == 6) {
    int new_nft_item_amount = in_msg_body~load_uint(64);
    save_data(owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, is_verified, new_nft_item_amount, max_supply);
    return ();
  }

  ;; Change royalty params
  if (op == 7) {
    cell new_royalty_params = in_msg_body~load_ref();
    save_data(owner_address, next_item_index, content, nft_item_code, new_royalty_params, minting_price, is_verified, nft_item_amount, max_supply);
    return ();
  }

  ;; Change Max Supply
  if (op == 8) {
    int new_max_supply = in_msg_body~load_uint(64);
    save_data(owner_address, next_item_index, content, nft_item_code, royalty_params, minting_price, is_verified, nft_item_amount, new_max_supply);
    return ();
  }
  
  ;; Emergency Withdraw
  if (op == 10) {
      ;; Ensure min storage remains
      raw_reserve(20000000, 0); ;; Reserve 0.02 TON
      
      var msg = begin_cell()
        .store_uint(0x10, 6) ;; no bounce
        .store_slice(owner_address)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(0, 32)
        .store_slice("Emergency Withdraw")
        .end_cell();
      
      send_raw_message(msg, 128); ;; Send remaining balance
      return ();
  }

    throw(0xffff);
}

;; Get methods

(int, cell, slice) get_collection_data() method_id {
  var (owner_address, next_item_index, content, _, _, _, _, _, _) = load_data();
  slice cs = content.begin_parse();
  return (next_item_index, cs~load_ref(), owner_address);
}

slice get_nft_address_by_index(int index) method_id {
    var (_, _, _, nft_item_code, _, _, _, _, _) = load_data();
    cell state_init = calculate_nft_item_state_init(index, nft_item_code);
    return calculate_nft_item_address(0, state_init);
}

(int, int, slice) royalty_params() method_id {
     var (_, _, _, _, royalty, _, _, _, _) = load_data();
     slice rs = royalty.begin_parse();
     return (rs~load_uint(16), rs~load_uint(16), rs~load_msg_addr());
}

int get_minting_price() method_id {
    var (_, _, _, _, _, minting_price, _, _, _) = load_data();
    return minting_price;
}

int get_nft_item_amount() method_id {
    var (_, _, _, _, _, _, _, nft_item_amount, _) = load_data();
    return nft_item_amount;
}

int is_verified() method_id {
    var (_, _, _, _, _, _, is_verified, _, _) = load_data();
    return is_verified;
}

int get_max_supply() method_id {
    var (_, _, _, _, _, _, _, _, max_supply) = load_data();
    return max_supply;
}

cell get_nft_content(int index, cell individual_nft_content) method_id {
  var (_, _, content, _, _, _, _, _, _) = load_data();
  slice cs = content.begin_parse();
  cs~load_ref();
  slice common_content = cs~load_ref().begin_parse();
  return (begin_cell()
                      .store_uint(1, 8) ;; offchain tag
                      .store_slice(common_content)
                      .store_ref(individual_nft_content)
          .end_cell());
}
